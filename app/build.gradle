apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'

def keystorePSW = ''
def keystoreAlias = ''
def keystoreAliasPSW = ''
// default keystore file, PLZ config file path in local.properties
def keyfile = null
Properties properties = new Properties()
// local.properties file in the root director
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def keystoreFilepath = properties.getProperty("storeFile")

if (keystoreFilepath) {
    keystorePSW = properties.getProperty("keyPassword")
    keystoreAliasPSW = properties.getProperty("storePassword")
    keystoreAlias = properties.getProperty("keyAlias")
    keyfile = file(keystoreFilepath)
}

def buglyKey = properties.getProperty("buglyKey")
def bdMapKey = properties.getProperty("bdMapKey")
def bdUpdateKey = properties.getProperty("bdUpdateKey")
def bdUpdateId = properties.getProperty("bdUpdateId")
def bdStatId = properties.getProperty("bdStatId")

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "tysheng.sxbus"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 38
        versionName "3.8"
        resConfigs "zh"
        ndk {
            //设置支持的SO库架构
            abiFilters 'armeabi-v7a'//, 'armeabi-v7a', 'x86', 'armeabi-v7a', 'x86_64'//, 'arm64-v8a'
        }
        multiDexEnabled = true

        buildConfigField "String", "BUGLY_KEY", buglyKey
        manifestPlaceholders = [BD_MAP_KEY   : bdMapKey,
                                BD_UPDATE_ID : bdUpdateId,
                                BD_UPDATE_KEY: bdUpdateKey,
                                BD_STAT_ID   : bdStatId]
    }
    signingConfigs {
        config {
            storeFile keyfile
            keyPassword keystorePSW
            storePassword keystoreAliasPSW
            keyAlias keystoreAlias
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main() {
            jniLibs.srcDirs = ['libs']
        }
    }
    dataBinding {
        enabled = true
    }
    //bugly
    // 编译选项
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    // recommend
    dexOptions {
        jumboMode = true
    }
    repositories {
        flatDir {
            dirs 'libs' //this way we can find the .aar file in libs folder
        }
    }
}
//greendao配置
greendao {
    schemaVersion 4                            //版本号，升级时可配置
    daoPackage 'tysheng.sxbus.dao'     //包名
    targetGenDir 'src/main/java'                 //生成目录
}
def androidSupportVersion = "25.3.1"
dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:' + androidSupportVersion
    compile 'com.android.support:recyclerview-v7:' + androidSupportVersion
    compile 'com.android.support:cardview-v7:' + androidSupportVersion
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.15'
    compile 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.4@aar'
    compile 'com.trello.rxlifecycle2:rxlifecycle:2.0.1'
    compile 'com.trello.rxlifecycle2:rxlifecycle-android:2.0.1'
    compile 'com.trello.rxlifecycle2:rxlifecycle-components:2.0.1'
    compile 'org.greenrobot:greendao:3.2.2'
    compile files('libs/BaiduLBS_Android.jar')
    compile 'com.alibaba:fastjson:1.1.56.android'
    compile 'com.google.dagger:dagger:2.11-rc2'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.11-rc2'
//    compile 'com.tencent.bugly:crashreport:2.6.0'
    compile 'com.tencent.bugly:crashreport_upgrade:1.3.0'
    //分包
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.android.support:customtabs:' + androidSupportVersion
}
// 依赖插件脚本
apply from: 'tinker-support.gradle'